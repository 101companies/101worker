#!/usr/bin/perl
use strict;
use warnings;
use File::Slurp   qw(write_file);
use LWP::Simple   qw(get);
use JSON          qw(encode_json decode_json);
use Repo101::Pull qw(pull101repo);

my %paths = map { ($_ => $ENV{$_} || die "Missing environment variable: $_") }
                qw(repo101dir gitdeps101dir repo101url gitdeps101url);

my $branches = exists $ENV{repo101branch}
             ? {$paths{repo101url} => $ENV{repo101branch}}
             : {};

my $repos = do
{
    my $url     = $paths{gitdeps101url};
    my $content = get($url) or die "Couldn't fetch $url";
    decode_json($content)   or die "Couldn't decode JSON: $content"
};

my $changes = pull101repo(
    root_path => $paths{repo101dir   },
    deps_path => $paths{gitdeps101dir},
    root_url  => $paths{repo101url   },
    repos     => $repos,
    branches  => $branches,
);

my @keys = sort keys %$changes;
for my $i (0 .. $#keys)
{
    my $path = $keys[$i];
    my $op   = $changes->{$path};
    print "$i $op $paths{repo101dir}/$path\n";
}
