#!/usr/bin/perl
use strict;
use warnings;
use Email::Simple;
use Email::Sender::Simple           qw(sendmail);
use Email::Sender::Transport::SMTPS;
use File::Slurp                     qw(slurp);
use POSIX                           qw(strftime);


my $logs_url = 'http://worker.101companies.org/logs';
my $template = slurp \*DATA; # see __DATA__ at the end
my $date     = slurp "../101web/logs/lastArchive";
my $logfile  = "../101web/logs/$date/runner.log";


my $mail = Email::Simple->create(
    header => [
        'From'         => 'softlang@uni-koblenz.de',
        'To'           => '101companies@gmail.com',
        'Subject'      => '[101worker] Execution Status Report',
        'Content-Type' => 'text/html',
    ],
    body   => sprintf $template, log_to_html($logfile),
);

#sendmail($mail, {transport => Email::Sender::Transport::SMTPS->new(
#    host => 'deliver.uni-koblenz.de',
#    ssl  => 1,
#)});
print $mail->body;


sub log_to_html
{
    my ($file) = @_;
    my  $table = '';

    open my $log, '<', $file or return ("can't open $file: $!", $table);

    my $last_line;
    while (<$log>)
    {
        $last_line = $_;
        next unless /^report:/;
        my (undef, $module, $exit_code, $reason, $start, $end) = split /\t/;

        my $status    = $exit_code == 0 ? $reason : "<strong>$reason</strong>";
        my $start_hms = strftime('%H:%M:%S', localtime $start    );
        my   $end_hms = strftime('%H:%M:%S', localtime $end      );
        my $taken_hms = strftime('%H:%M:%S', gmtime $end - $start);

        $table .= <<HTML;
                <tr>
                    <td><a href="$logs_url/$date/$module.log">$module</a></td>
                    <td>$status</td>
                    <td>$start_hms</td>
                    <td>$end_hms</td>
                    <td>$taken_hms</td>
                </tr>
HTML
    }

    my $message = $last_line =~ /report-ok/
                ? 'runner says ok'
                : "<em>runner didn't say ok</em>, please check if "
                . "it's fallen over or caught fire or something";
    ($message, $table)
}


__DATA__
<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <style>
            td, th
            {
                padding-left  : 0.5em;
                padding-right : 0.5em;
                text-align    : center;
            }
        </style>
    </head>
    <body>
        <h1>101worker Execution Status Report</h1>
        <p><strong>Sanity:</strong> %s</p>
        <table>
            <thead>
                <tr>
                    <th>Module</th>
                    <th>Status</th>
                    <th>Started</th>
                    <th>Ended</th>
                    <th>Time Taken</th>
                </tr>
            </thead>
            <tbody>
%s
            </tbody>
        </table>
    </body>
</html>
